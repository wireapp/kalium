apply plugin: "io.gitlab.arturbosch.detekt"

detekt {
    buildUponDefaultConfig = true
    // activate all available (even unstable) rules.
    // allRules = false
    config = files("$projectDir/detekt/detekt.yml")
    input = files("$projectDir")
    // a way of suppressing issues before introducing detekt
    baseline = file("$projectDir/detekt/baseline.xml")

    // dynamic prop to enable and disable autocorrect, enabled locally via local.properties file
    def autoFixEnabled = ProjectExtensionsKt.getLocalProperty("detektAutofix", false, project)
    autoCorrect = Boolean.getBoolean(autoFixEnabled)
    println("> Detekt autoCorrect: " + autoCorrect)

    reports {
        html.enabled = true // observe findings in your browser with structure and code snippets
        xml.enabled = true
        txt.enabled = true
    }
}

dependencies {
    // Can't add using the full version "Dependencies.Detekt.detektCli" (xml error on sync)
    def catalogs = project.extensions.getByType(VersionCatalogsExtension)
    detekt "io.gitlab.arturbosch.detekt:detekt-cli:${catalogs.first().findVersion("detekt").get()}"
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:${catalogs.first().findVersion("detekt").get()}"
    detektPlugins "com.wire:detekt-rules:1.0.0-SNAPSHOT" changing(true)
}

configurations {
    all {
        resolutionStrategy {
            eachDependency { DependencyResolveDetails details ->
                if (requested.group == "com.wire" && requested.name == "detekt-rules") {
                    // disable cache for jar dependency, if new version is available it will be downloaded
                    cacheChangingModulesFor(0, "SECONDS")
                }
            }
        }
    }
}
